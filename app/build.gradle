apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 1
def versionMinor = 3
def versionPatch = 0

// Hat tip to Jake Wharton for this - found it in the u2020 app
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        resValue "string", "app_name", "PremoFM"
        applicationId "com.mainmethod.premofm"
        minSdkVersion 21
        targetSdkVersion 23
        renderscriptTargetApi 21
        versionCode 100036
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionCode}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        setProperty("archivesBaseName", "PremoFM-$versionName")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            buildConfigField "String", "API_ENV", "\"qa\""
            buildConfigField "String", "API_PROTOCOL", "\"https\""
            buildConfigField "String", "API_HOSTNAME", "\"\""
            buildConfigField "String", "API_PORT", "\"443\""
            buildConfigField "String", "API_PASSKEY", "\"\""
            buildConfigField "String", "GCM_SENDER_ID", "\"\""
            buildConfigField "String", "CAST_APP_ID", "\"\""
            versionNameSuffix "-DEBUG"
            //signingConfig signingConfigs.release
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "PremoFM Debug"
        }
        release {
            buildConfigField "String", "API_ENV", "\"prod\""
            buildConfigField "String", "API_PROTOCOL", "\"https\""
            buildConfigField "String", "API_HOSTNAME", "\"\""
            buildConfigField "String", "API_PORT", "\"443\""
            buildConfigField "String", "API_PASSKEY", "\"\""
            buildConfigField "String", "GCM_SENDER_ID", "\"\""
            buildConfigField "String", "CAST_APP_ID", "\"\""
            minifyEnabled true
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //signingConfig signingConfigs.release
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Unit testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'

    // Support libraries
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:mediarouter-v7:23.1.1'

    // Google Play Services
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-cast:8.4.0'

    // 3rd party imports
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.5'
    compile 'com.github.amlcurran.showcaseview:library:5.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.makeramen:roundedimageview:2.2.0'
    compile 'org.parceler:parceler-api:1.0.4'
    provided 'org.parceler:parceler:1.0.4'
}
